apply plugin: "com.android.application"
apply plugin: "com.google.android.gms.oss-licenses-plugin"
apply plugin: "androidx.navigation.safeargs"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlinx-serialization"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "io.gitlab.arturbosch.detekt"

/*
 *    Copyright 2019 Andrey Mukamolov
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

def keystoreProperties = PropertiesLoaderKt.loadProperties(rootProject.file("keystore.properties"))

def apiProperties = PropertiesLoaderKt.loadProperties(rootProject.file("api.properties"))

android {
  compileSdkVersion 29
  signingConfigs {
    releaseConfig {
      keyAlias keystoreProperties["keyAlias"]
      keyPassword keystoreProperties["keyPassword"]
      storeFile file(keystoreProperties["storeFile"])
      storePassword keystoreProperties["storePassword"]
    }
  }
  defaultConfig {
    applicationId "com.bookcrossing.mobile"
    minSdkVersion 17
    targetSdkVersion 29
    versionCode 10
    versionName "1.5.1"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    multiDexEnabled true

    manifestPlaceholders = [admob: apiProperties["admob_app_id"]]

    resValue "string", "google_maps_key", apiProperties["google_maps_key"]
    resValue "string", "mapbox_access_token", apiProperties["mapbox_access_token"]
    resValue "string", "admob_publisher_id", apiProperties["admob_publisher_id"]

    vectorDrawables.useSupportLibrary = true
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
      signingConfig signingConfigs.releaseConfig

      manifestPlaceholders = [admob: apiProperties["admob_app_id"]]

      resValue "string", "admob_ad_unit_id", apiProperties["admob_ad_unit_id"]
    }
    debug {
      manifestPlaceholders = [admob: "ca-app-pub-3940256099942544~3347511713"] // test values

      resValue "string", "admob_ad_unit_id", "ca-app-pub-3940256099942544/6300978111"
      
      ext.alwaysUpdateBuildId = false
      firebaseCrashlytics {
        // If you don't need crash reporting for your debug build,
        // you can speed up your build by disabling mapping file uploading.
        mappingFileUploadEnabled false
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  kapt {
    useBuildCache = true
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  packagingOptions {
    exclude "META-INF/rxjava.properties"
    exclude "META-INF/*.kotlin_module"
  }
}

dependencies {
  implementation fileTree(include: ["*.jar"], dir: "libs")
  testImplementation "junit:junit:4.13"
  testImplementation "org.mockito:mockito-core:3.3.0"
  testImplementation "io.mockk:mockk:1.9.3"
  testImplementation "org.hamcrest:hamcrest:2.2"
  testImplementation "androidx.test:core:1.2.0"
  androidTestImplementation "androidx.test.ext:junit:1.1.1"
  androidTestImplementation "org.mockito:mockito-android:3.3.0"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  def moxyVersion = "2.1.1"

  implementation "com.github.moxy-community:moxy:$moxyVersion"
  implementation "com.github.moxy-community:moxy-android:$moxyVersion"
  implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
  implementation "com.github.moxy-community:moxy-ktx:$moxyVersion"

  implementation "com.google.firebase:firebase-auth:19.3.1"
  implementation "com.google.firebase:firebase-messaging:20.1.7"
  implementation "com.google.firebase:firebase-ads:19.1.0"
  implementation "com.google.firebase:firebase-crashlytics:17.0.0"
  implementation "com.google.android.ads.consent:consent-library:1.0.8"

  implementation "com.google.android.gms:play-services-maps:17.0.0"
  implementation "com.google.android.gms:play-services-location:17.0.0"
  implementation "com.google.android.gms:play-services-auth:18.0.0"
  implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

  implementation "com.google.android.material:material:1.2.0-alpha06"
  implementation "androidx.cardview:cardview:1.0.0"
  implementation "androidx.preference:preference-ktx:1.1.1"

  def androidxCoreVersion = "1.3.0-alpha02"
  implementation "androidx.core:core:$androidxCoreVersion"
  implementation "androidx.core:core-ktx:$androidxCoreVersion"

  implementation "androidx.fragment:fragment-ktx:1.2.4"
  implementation "androidx.vectordrawable:vectordrawable:1.1.0"
  implementation "androidx.appcompat:appcompat:1.1.0"
  implementation "androidx.recyclerview:recyclerview:1.1.0"

  implementation "dev.chrisbanes:insetter-ktx:0.2.0"

  implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
  implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

  def lifecycleVersion = "2.2.0"
  implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
  implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

  def rxBindingsVersion = "3.1.0"
  implementation "com.jakewharton.rxbinding3:rxbinding:$rxBindingsVersion"
  implementation "com.jakewharton.rxbinding3:rxbinding-material:$rxBindingsVersion"
  implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rxBindingsVersion"
  implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBindingsVersion"

  def daggerVersion = "2.26"
  implementation "com.google.dagger:dagger:$daggerVersion"

  def glideVersion = "4.11.0"
  implementation "com.github.bumptech.glide:glide:$glideVersion"

  implementation "io.reactivex.rxjava2:rxjava:2.2.18"
  implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
  implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

  def permissionsVersion = "1.1.2"
  implementation "com.github.florent37:runtime-permission:$permissionsVersion"
  implementation "com.github.florent37:runtime-permission-rx:$permissionsVersion"

  def firebaseUiVersion = "6.2.0"
  implementation "com.firebaseui:firebase-ui:$firebaseUiVersion"
  implementation "com.firebaseui:firebase-ui-storage:$firebaseUiVersion"
  implementation "com.firebaseui:firebase-ui-database:$firebaseUiVersion"
  implementation "com.firebaseui:firebase-ui-auth:$firebaseUiVersion"
  implementation "com.github.frangsierra:rx2firebase:1.5.7"

  def butterknifeVersion = "10.2.1"
  implementation "com.jakewharton:butterknife:$butterknifeVersion"

  implementation "androidx.constraintlayout:constraintlayout:1.1.3"
  implementation "androidx.multidex:multidex:2.0.1"

  implementation "com.jakewharton.timber:timber:4.7.1"

  def materialDialogsVersion = "3.1.1"
  implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
  implementation "com.afollestad.material-dialogs:input:$materialDialogsVersion"

  implementation "com.github.curioustechizen.android-ago:library:1.4.0"

  implementation "com.google.zxing:android-integration:3.3.0"
  implementation "com.google.zxing:android-core:3.3.0"
  implementation "com.google.zxing:core:3.4.0"
  implementation "com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3"

  def mapboxSdkVersion = "5.0.0"
  implementation "com.mapbox.mapboxsdk:mapbox-sdk-core:$mapboxSdkVersion"
  implementation "com.mapbox.mapboxsdk:mapbox-sdk-services:$mapboxSdkVersion"

  implementation "com.algolia:instantsearch-android:2.2.1"

  debugImplementation "com.squareup.leakcanary:leakcanary-android:2.2"

  kapt "com.google.dagger:dagger-compiler:$daggerVersion"
  kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"
  kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"
  kapt "com.github.bumptech.glide:compiler:$glideVersion"
}

apply plugin: "com.google.gms.google-services"
